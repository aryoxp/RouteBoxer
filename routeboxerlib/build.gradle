apply plugin: 'com.android.library'
apply plugin: 'com.github.dcendents.android-maven'
apply plugin: 'com.jfrog.bintray'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile 'com.google.android.gms:play-services:8.3.0'
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'ap.mobile.douglaspeucker:douglaspeucker:1.0.1'

}

task sourcesJar(type: Jar) {
    from android.sourceSets.main.java.srcDirs
    classifier = 'sources'
}

task javadoc(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

group = 'ap.mobile.routeboxerlib'
version = '1.0.1'

install {
    repositories.mavenInstaller {
        pom.project {
            name 'RouteBoxer'
            description 'Am implementation library of Google RouteBoxer algorithm'
            url 'https://github.com/aryoxp/routeboxer'
            inceptionYear '2017'

            packaging 'aar'
            groupId 'ap.mobile.routeboxerlib'
            artifactId 'routeboxer'
            version '1.0.1'

            licenses {
                license {
                    name 'The MIT License (MIT)'
                    url 'https://opensource.org/licenses/MIT'
                }
            }
            scm {
                connection 'https://github.com/aryoxp/RouteBoxer.git'
                url 'https://github.com/aryoxp/RouteBoxer'

            }
            developers {
                developer {
                    id = 'aryoxp'
                    name 'Aryo'
                }
            }
        }
    }
}

Properties bintrayProperties = new Properties()
bintrayProperties.load(project.rootProject.file('bintray.properties').newDataInputStream())

bintray {
    user = bintrayProperties.getProperty('user')
    key = bintrayProperties.get('key')
    configurations = ['archives']
    pkg {
        repo = 'RouteBoxer'
        name = 'RouteBoxer'
        userOrg = 'aryoxp'
        licenses = ['MIT']
        vcsUrl = 'https://github.com/aryoxp/RouteBoxer.git'
        publish = true
        version {
            name = 'v1.0.1'
            desc = 'RouteBoxer 1.0'
            released = new Date()
            vcsTag = 'v1.0.1'
        }
    }
}
